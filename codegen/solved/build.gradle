plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.openapi.generator" version "7.0.1"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// require in this swagger case
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.16'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

apply plugin: 'org.openapi.generator'

openApiGenerate {
	generatorName.set("spring")
	inputSpec.set("$rootDir/specs/petstore.yaml")
	outputDir.set(project.layout.buildDirectory.file("generated/$name").map { it.asFile.absolutePath })
	modelPackage.set("com.aorjoa.codegen.model")
	apiPackage.set("com.aorjoa.codegen.api")
	invokerPackage.set("com.example.demo.invoker")
	cleanupOutput.set(true)

	// see also: https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/java.md
	configOptions.set([
			dateLibrary: "java8",
			hideGenerationTimestamp: "true",
			interfaceOnly: "true"
	])
}

sourceSets {
	main {
		java {
			srcDir(tasks.openApiGenerate)
		}
	}
}